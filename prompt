#!/usr/bin/env perl

use 5.18.2;
use strict;
use warnings;
use utf8;
use Encode;
use Text::CharWidth qw(mbswidth);
use Getopt::Long;
use Pod::Usage;

our $VERSION = "1.2.0-snapshot";

my $encoding = `locale charmap`;
my $date;
my $pwd = decode($encoding, $ENV{"PWD"});
my $home = decode($encoding, $ENV{"HOME"});
my $dateFormat;
my $columns;
my $help = 0;
my $pwdColorName = "green";
my $dateColorName = "red";

binmode(STDOUT, ":encoding($encoding)");

$columns = 80;

Getopt::Long::Configure(
    "auto_version",
    "require_order",
    "no_ignore_case",
    "no_getopt_compat"
);
GetOptions (
    'help' => \$help,
    'columns=i' => \$columns,
    'pwd-color=s' => \$pwdColorName,
    'date-color=s' => \$dateColorName,
    'date-fmt=s' => \$dateFormat
) or pod2usage (2);
pod2usage (-verbose => 2) if $help;

if (defined $dateFormat) {
    $date = `date "+$dateFormat"`;
} else {
    $date = `date`;
}
$date = decode ($encoding, $date);
$date =~ s/\s*$//g; # `date` normaly ends with a "\n"
$date =~ s/^(\w)/\u$1/;
$pwd =~ s/^$home/~/;
my $space = $columns - mbswidth($date) - mbswidth($pwd);

# ANSI terminal colors:
sub ansiColor {
    my $colorName = shift;
    for ($colorName) {
        if (/^black$/)   { return "\e[30m"; }
        if (/^red$/)     { return "\e[31m"; }
        if (/^green$/)   { return "\e[32m"; }
        if (/^yellow$/)  { return "\e[33m"; }
        if (/^blue$/)    { return "\e[34m"; }
        if (/^magenta$/) { return "\e[35m"; }
        if (/^cyan$/)    { return "\e[36m"; }
        if (/^white$/)   { return "\e[37m"; }
        default          { return "\e[0m"; }
    }
}

my $pwdColor   = ansiColor ($pwdColorName);
my $dateColor  = ansiColor ($dateColorName);
my $resetColor = ansiColor ("reset");

if ( $space > 0 ) {
    print
        $pwdColor, $pwd,
        $resetColor, (" " x $space),
        $dateColor, $date,
        $resetColor, "\n";
} elsif ( mbswidth($pwd) <= $columns ) {
    print $pwdColor, $pwd, $resetColor, "\n";
} else {
    while (mbswidth($pwd) >= $columns) {
        $pwd =~ s/^\X//;
    }
    print $pwdColor, "…", $pwd, $resetColor, "\n";
}

__END__

=encoding UTF-8

=head1 NAME

prompt - pretty shell prompt

=head1 SYNOPSIS

prompt [options]

 Options:
   --version            print version
   --help               full documentation
   --columns NUM        column width of the terminal
   --pwd-color COLOR    color to output working directory
   --date-color COLOR   color to output date
   --date-fmt STRING    date format

Options can be abbrieviated to uniqueness.

=head1 OPTIONS

=over

=item B<--version>

Print version information and exit.

=item B<--help>

Print the manual page and exits.

=item B<--columns NUM>

Set the number of columns to output. Should be your terminal’s width.

=item B<--pwd-color>

Set the color to use when printing current directory. See B<--date-color>
for supported colors.

=item B<--date-color>

Set the color to use when printing the current date. The supported colors are:

=over

=item black

=item red

=item green

=item yellow

=item blue

=item magenta

=item cyan

=item white

=item default

=back

B<default> being the default color of your terminal.

=item B<--date-fmt STRING>

Specify how to format the date. It must be a valid output format for the
B<date> command, without the leading '+'.

=back

=head1 DESCRIPTION

B<prompt> will allow you to display the full path of your working directory
on a separate line, just above the shell’s prompt. This mean you can have
a short prompt, while having the working directory printed.

If there is enough place, B<prompt> will also display the current date after
the working directory.

If there is not enough place to display the working directory on one line,
B<prompt> will only display the end of it.
